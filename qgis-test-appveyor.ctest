SET (CTEST_PROJECT_NAME "QGIS")
SET (CTEST_SOURCE_DIRECTORY $ENV{APPVEYOR_BUILD_FOLDER})
SET (CTEST_BINARY_DIRECTORY "$ENV{APPVEYOR_BUILD_FOLDER}/ms-windows/osgeo4w/build")
SET (CTEST_SITE "appveyor")

FILE(MAKE_DIRECTORY ${CTEST_BINARY_DIRECTORY})

IF ($ENV{APPVEYOR_PULL_REQUEST_NUMBER})
  SET (CTEST_BUILD_NAME "PR: $ENV{APPVEYOR_PULL_REQUEST_NUMBER} / $ENV{APPVEYOR_REPO_BRANCH} ($ENV{APPVEYOR_REPO_COMMIT}})")
ELSE($ENV{APPVEYOR_PULL_REQUEST_NUMBER})
  SET (CTEST_BUILD_NAME "$ENV{APPVEYOR_REPO_BRANCH} ($ENV{APPVEYOR_REPO_COMMIT})")
ENDIF()

IF($ENV{PLATFORM} STREQUAL "x86")
	SET(ENV{OSGEO4W_ROOT} "C:\\OSGeo4W")
	SET(O4W_ROOT "C:/OSGeo4W")
	SET(CTEST_CMAKE_GENERATOR "Visual Studio 10")

	SET (INITIAL_CACHE "
		SIP_BINARY_PATH:STRING=${O4W_ROOT}/apps/Python27/sip.exe
		QWT_LIBRARY:STRING=${O4W_ROOT}/lib/qwt.lib
		WITH_GRASS:BOOL=ON
		WITH_GRASS7:BOOL=ON
		GRASS_PREFIX:STRING=${O4W_ROOT}/apps/grass/grass-6.4.4
		GRASS_PREFIX7:STRING=${O4W_ROOT}/apps/grass/grass-7.0.1RC1
		CMAKE_CXX_FLAGS_RELWITHDEBINFO:STRING=\"/MD /ZI /MP /Od /D NDEBUG /D QGISDEBUG\"
	")
ELSE($ENV{PLATFORM} STREQUAL "x86")
	SET(ENV{OSGEO4W_ROOT} "C:\\OSGeo4W64")
	SET(O4W_ROOT "C:/OSGeo4W64")
	SET(CTEST_CMAKE_GENERATOR "Visual Studio 10 Win64")

	SET (INITIAL_CACHE "
		SPATIALINDEX_LIBRARY:STRING=${O4W_ROOT}/lib/spatialindex-64.lib
		WITH_GRASS:BOOL=ON
		WITH_GRASS7:BOOL=OFF
		GRASS_PREFIX:STRING=${O4W_ROOT}/apps/grass/grass-6.4.3
		SIP_BINARY_PATH:STRING=${O4W_ROOT}/bin/sip.exe
		QWT_LIBRARY:STRING=${O4W_ROOT}/lib/qwt5.lib
		CMAKE_CXX_FLAGS_RELWITHDEBINFO:STRING=\"/MD /Zi /MP /Od /D NDEBUG /D QGISDEBUG\"
		SETUPAPI_LIBRARY:STRING=\"C:\\Program Files\\Microsoft SDKs\\Windows\\v7.1\\Lib\\x64\\SetupAPI.Lib\"
		CMAKE_INSTALL_SYSTEM_RUNTIME_LIBS_NO_WARNINGS:BOOL=OFF
	")
ENDIF($ENV{PLATFORM} STREQUAL "x86")

SET (INITIAL_CACHE "${INITIAL_CACHE}
  BUILDNAME:STRING=${CTEST_BUILD_NAME}
  CTEST_USE_LAUNCHERS:BOOL=ON
  PEDANTIC:BOOL=ON
  WITH_QSPATIALITE:BOOL=ON
  WITH_SERVER:BOOL=ON
  SERVER_SKIP_ECW:BOOL=ON
  WITH_ASTYLE:BOOL=ON
  WITH_GLOBE:BOOL=ON
  WITH_TOUCH:BOOL=ON
  WITH_ORACLE:BOOL=ON
  WITH_CUSTOM_WIDGETS:BOOL=ON
  ENABLE_PGTEST:BOOL=ON
  GEOS_LIBRARY:STRING:STRING=${O4W_ROOT}/lib/geos_c.lib
  SQLITE3_LIBRARY:STRING=${O4W_ROOT}/lib/sqlite3_i.lib
  SPATIALITE_LIBRARY:STRING=${O4W_ROOT}/lib/spatialite_i.lib
  PYTHON_EXECUTABLE:STRING=${O4W_ROOT}/bin/python.exe
  PYTHON_INCLUDE_PATH:STRING=${O4W_ROOT}/apps/Python27/include
  PYTHON_LIBRARY:STRING=${O4W_ROOT}/apps/Python27/libs/python27.lib
  QT_BINARY_DIR:STRING=${O4W_ROOT}/bin
  QT_LIBRARY_DIR:STRING=${O4W_ROOT}/lib
  QT_HEADERS_DIR:STRING=${O4W_ROOT}/include/qt4
  QWT_INCLUDE_DIR:STRING=${O4W_ROOT}/include/qwt
  FCGI_INCLUDE_DIR:STRING=${O4W_ROOT}/include
  FCGI_LIBRARY:STRING=${O4W_ROOT}/lib/libfcgi.lib
  WITH_INTERNAL_JINJA2:BOOL=OFF
  WITH_INTERNAL_MARKUPSAFE:BOOL=OFF
  WITH_INTERNAL_PYGMENTS:BOOL=OFF
  WITH_INTERNAL_DATEUTIL:BOOL=OFF
  WITH_INTERNAL_PYTZ:BOOL=OFF
  WITH_INTERNAL_SIX:BOOL=OFF
")

SET (CTEST_CMAKE_COMMAND "cmake" )
SET (CTEST_BUILD_CONFIGURATION "RelWithDebInfo")

FILE(WRITE "${CTEST_BINARY_DIRECTORY}/CMakeCache.txt" ${INITIAL_CACHE})

# ${OSGEO4W_ROOT}\etc\ini
SET(ENV{LIB_DIR} $ENV{OSGEO4W_ROOT})
SET(ENV{INCLUDE} $ENV{INCLUDE};${OSGEO4W_ROOT}\\include)
SET(ENV{LIB} $ENV{LIB};${OSGEO4W_ROOT}\\lib)
SET(ENV{GDAL_DATA} ${OSGEO4_ROOT}\\share\\gdal)
SET(ENV{GDAL_DRIVER_PATH} ${OSGEO4W_ROOT}\\bin\\gdalplugins)
SET(ENV{GEOTIFF_CSV} ${OSGEO4W_ROOT}\\share\\epsg_csv)
SET(ENV{PROJ_LIB} ${OSGEO4W_ROOT}\\share\\proj)
SET(ENV{PYTHONHOME} ${OSGEO4W_ROOT}\\apps\\Python27)
SET(ENV{PATH} $ENV{PATH};${OSGEO4W_ROOT}\\apps\\Python27\\Scripts;${OSGEO4W_ROOT}\\apps\\msys\\bin)
SET(ENV{QT_PLUGIN_PATH} ${OSGEO4W_ROOT}\\apps\\qt4\\plugins)
SET(ENV{QT_RASTER_CLIP_LIMIT} 4096)

SET (CTEST_NOTES_FILES
  ${CTEST_SCRIPT_DIRECTORY}/${CTEST_SCRIPT_NAME}
  ${CTEST_BINARY_DIRECTORY}/CMakeCache.txt
)

IF(NOT WIN32)
  STRING(ASCII 27 Esc)
  SET(ColorReset  "${Esc}[m")
  SET(ColorBold   "${Esc}[1m")
  SET(Red         "${Esc}[31m")
  SET(Green       "${Esc}[32m")
  SET(Yellow      "${Esc}[33m")
  SET(Blue        "${Esc}[34m")
  SET(Magenta     "${Esc}[35m")
  SET(Cyan        "${Esc}[36m")
  SET(White       "${Esc}[37m")
  SET(BoldRed     "${Esc}[1;31m")
  SET(BoldGreen   "${Esc}[1;32m")
  SET(BoldYellow  "${Esc}[1;33m")
  SET(BoldBlue    "${Esc}[1;34m")
  SET(BoldMagenta "${Esc}[1;35m")
  SET(BoldCyan    "${Esc}[1;36m")
  SET(BoldWhite   "${Esc}[1;37m")
ENDIF(NOT WIN32)

ctest_start(Experimental)
ctest_configure(RETURN_VALUE CONFRES)
IF(NOT ${CONFRES} EQUAL 0)
  ctest_submit (RETRY_COUNT 3 RETRY_DELAY 30)
  MESSAGE( FATAL_ERROR "${Red}Configure failed.${ColorReset}" )
ENDIF(NOT ${CONFRES} EQUAL 0)
ctest_build (RETURN_VALUE BUILDRES NUMBER_WARNINGS NUMWARN NUMBER_ERRORS NUMERR)
IF(NOT ${BUILDRES} EQUAL 0 OR NOT ${NUMERR} EQUAL 0)
  ctest_submit (RETRY_COUNT 3 RETRY_DELAY 30)
  MESSAGE("${Yellow}Test results submitted to${ColorReset}")
  MESSAGE("${BoldYellow}http://dash.orfeo-toolbox.org/index.php?project=QGIS&filtercount=1&showfilters=1&field1=buildname/string&compare1=63&value1=$ENV{APPVEYOR_REPO_COMMIT}${ColorReset}" )
  MESSAGE( FATAL_ERROR "${Red}Build failed.${ColorReset}" )
ENDIF(NOT ${BUILDRES} EQUAL 0 OR NOT ${NUMERR} EQUAL 0)
IF(NOT ${NUMWARN} EQUAL 0)
  ctest_submit (RETRY_COUNT 3 RETRY_DELAY 30)
  MESSAGE("${Yellow}Test results submitted to${ColorReset}")
  MESSAGE("${BoldYellow}http://dash.orfeo-toolbox.org/index.php?project=QGIS&filtercount=1&showfilters=1&field1=buildname/string&compare1=63&value1=$ENV{APPVEYOR_REPO_COMMIT}${ColorReset}" )
  MESSAGE( FATAL_ERROR "${Red}Build warnings found, aborting test.${ColorReset}" )
ENDIF(NOT ${NUMWARN} EQUAL 0)
ctest_test (RETURN_VALUE TESTRES)
IF(NOT ${TESTRES} EQUAL 0)
  ctest_submit (RETRY_COUNT 3 RETRY_DELAY 30)
  MESSAGE("${Yellow}Test results submitted to${ColorReset}")
  MESSAGE("${BoldYellow}http://dash.orfeo-toolbox.org/index.php?project=QGIS&filtercount=1&showfilters=1&field1=buildname/string&compare1=63&value1=$ENV{APPVEYOR_REPO_COMMIT}${ColorReset}" )
  MESSAGE( FATAL_ERROR "Tests failed" )
ENDIF(NOT ${TESTRES} EQUAL 0)

ctest_submit (RETRY_COUNT 3 RETRY_DELAY 30)
MESSAGE("${Green}Test results submitted to${ColorReset}")
MESSAGE("${BoldGreen}http://dash.orfeo-toolbox.org/index.php?project=QGIS&filtercount=1&showfilters=1&field1=buildname/string&compare1=63&value1=$ENV{APPVEYOR_REPO_COMMIT}${ColorReset}" )
